# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//third_party/vk-gl-cts/vk_gl_cts.gni")

config("deqp_spirvtool_link_config") {
  cflags_cc = deqp_common_cflags_cc
  cflags_cc += [ "-ftemplate-depth=1024" ]
  defines = deqp_common_defines
  defines += [
    "SPIRV_CHECK_CONTEXT",
    "SPIRV_COLOR_TERMINAL",
    "SPIRV_LINUX",
    "SPIRV_TIMER_ENABLED",
  ]
}

config("spv_headers_public_config") {
  include_dirs = [ "include" ]
}

config("spvtools_include_gen_dirs") {
  include_dirs = [ "$target_gen_dir" ]
}

config("spvtools_internal_config") {
  include_dirs = [
    ".",
    "//third_party/spirv-headers/include",
  ]

  configs = [
    ":spv_headers_public_config",
    ":spvtools_include_gen_dirs",
  ]

  cflags = []
  if (is_clang) {
    cflags += [
      "-Wno-implicit-fallthrough",
      "-Wno-newline-eof",
      "-Wno-unreachable-code-break",
      "-Wno-unreachable-code-return",
    ]
  } else if (!is_win) {
    # Work around a false-positive on a Skia GCC 10 builder.
    cflags += [ "-Wno-format-truncation" ]
  } else {
    # Make MSVC report the correct value for __cplusplus
    cflags += [ "/Zc:__cplusplus" ]
  }

  if (!is_win) {
    cflags += [ "-std=c++17" ]
  } else {
    cflags += [ "/std:c++17" ]
  }
}

ohos_source_set("deqp_spirvtool_link_source") {
  sources = [
    "//third_party/spirv-tools/include/spirv-tools/instrument.hpp",
    "//third_party/spirv-tools/include/spirv-tools/libspirv.h",
    "//third_party/spirv-tools/include/spirv-tools/libspirv.hpp",
    "//third_party/spirv-tools/include/spirv-tools/linker.hpp",
    "//third_party/spirv-tools/include/spirv-tools/optimizer.hpp",
    "//third_party/spirv-tools/source/link/linker.cpp",
  ]

  include_dirs = deqp_common_include_dirs
  include_dirs += [
    "//third_party/spirv-tools",
    "//third_party/vk-gl-cts/build/external/spirv-tools/spirv-tools",
    "//third_party/spirv-headers/include",
    "//third_party/spirv-headers/include/spirv/unified1",
    "//third_party/spirv-tools/include",
  ]

  if (build_with_chromium) {
    configs -= [ "//build/config/compiler:chromium_code" ]
    configs += [ "//build/config/compiler:no_chromium_code" ]
  }

  configs = [ ":deqp_spirvtool_link_config" ]
  configs += [ ":spv_headers_public_config" ]
  configs += [ ":spvtools_internal_config" ]
}

ohos_static_library("libdeqp_spirvtools-link") {
  deps = [
    ":deqp_spirvtool_link_source",
    "//third_party/spirv-tools:libdeqp_spirvtools",
    "//third_party/spirv-tools/source/opt:libdeqp_spirvtools-opt",
    "//third_party/spirv-tools/source/val:libdeqp_spirvtools-val",
  ]
  part_name = "graphic_2d"
  subsystem_name = "graphic"
}
